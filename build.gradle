plugins {
    id 'java'
}

group = 'com.example'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    // ========================
    // UI Testing
    // ========================
    implementation 'org.seleniumhq.selenium:selenium-java:4.5.3'
    implementation 'io.github.bonigarcia:webdrivermanager:5.9.2'
    implementation 'org.seleniumhq.selenium:selenium-devtools-v104:4.5.3'

    // Cucumber + JUnit 4
    testImplementation 'io.cucumber:cucumber-java:7.15.0'
    testImplementation 'io.cucumber:cucumber-junit:7.15.0'
    testImplementation 'junit:junit:4.13.2'

    // ========================
    // API Testing
    // ========================
    testImplementation 'io.rest-assured:rest-assured:5.4.0'
    testImplementation 'org.assertj:assertj-core:3.25.3'

    // Optional Logging & JSON
    testImplementation 'com.fasterxml.jackson.core:jackson-databind:2.17.1'
    testImplementation 'org.slf4j:slf4j-simple:2.0.13'

    // Optional (jika pakai TestNG untuk API)
    testImplementation 'org.testng:testng:7.9.0'
}

test {
    // Ini untuk API test (pakai TestNG)
    useTestNG()
    testLogging {
        events "passed", "skipped", "failed"
    }
    reports {
        html.required.set(true)
        junitXml.required.set(true)
    }
}

// =============================
// TASK KHUSUS: Jalankan UI Test
// =============================
task runCucumber(type: Test) {
    useJUnit()

    // Gunakan semua test class (bukan hanya TestRunnerUI.class)
    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath

    // Optional: filter hanya UI test runner
    include '**/TestRunnerUI.class'

    testLogging {
        events "passed", "skipped", "failed"
    }

    reports {
        html.required.set(true)
        html.outputLocation.set(file("$buildDir/reports/tests/runCucumber"))
        junitXml.required.set(true)
    }
}
